#!/usr/bin/expect -f

# Configuration de base
set timeout 500
exp_internal 1
log_user 1
log_file -a /tmp/install_vtom_agent.log

# Vérification des arguments
if {$argc != 3} {
    puts "Usage: $argv0 <install_dir> <tmp_dir> <file1>"
    exit 1
}

set install_dir [lindex $argv 0]
set tmp_dir     [lindex $argv 1]
set file1       [lindex $argv 2]
set install_cmd [file join $tmp_dir "install_vtom"]

# Vérification que le fichier d'installation existe
if {![file exists $install_cmd]} {
    puts "Erreur: $install_cmd n'existe pas"
    exit 1
}

# Lancement de l'installation
spawn sudo $install_cmd $file1

expect {
    -re "Confirmez-vous ces parametres.*installation.*\\\[o\\\]" {
        send "o\r"
        exp_continue
    }
    -re "Repertoire de base d'installation.*\\\]" {
        send "$install_dir/vtom\r"
        exp_continue
    }
    -re "Definissez l'administrateur UNIX de Visual TOM.*\\\]" {
        send "vtom\r"
        exp_continue
    }
    -re "Repertoire des traces.*\\\]" {
        send "\r"
        exp_continue
    }
    -re "Repertoire des fichiers logs.*\\\]" {
        send "\r"
        exp_continue
    }
    -re "Sauvegarder l'installation existante.*\\\]" {
        send "n\r"
        exp_continue
    }
    -re "Numero de port tcp pour tomDBd.*\\\]" {
        send "30001\r"
        exp_continue
    }
    -re "Numero de port tcp pour vtmanager.*\\\]" {
        send "30000\r"
        exp_continue
    }
    -re "appuyez sur la touche Entree" {
        send "\r"
        expect eof
    }
    timeout {
        puts "Timeout atteint - installation échouée"
        exit 1
    }
    eof {
        puts "Installation terminée"
    }
}

# Attendre la fin du processus
wait
