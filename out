#!/usr/bin/expect -f

set timeout 200
exp_internal 1
log_user 1
log_file -a /tmp/install_expect.log

# Variables d'environnement passées en paramètre
set install_dir [lindex $argv 0]
set tmp_dir     [lindex $argv 1]
set file1       [lindex $argv 2]
set file2       [lindex $argv 3]

# Construction des chemins
set file1_path [file join $tmp_dir $file1]
set file2_path [file join $tmp_dir $file2]
set install_cmd [file join $tmp_dir "install_vtom"]

# Exécution
spawn sudo "$install_cmd" "$file1_path" "$file2_path"

expect {
    -re "Nom du serveur de backup.*\\[aucun\\]" {
        send "\r"
        exp_continue
    }
    -re "Repertoire de base d'installation.*\\]" {
        send "$install_dir\r"
        exp_continue
    }
    -re "Definissez l'administrateur UNIX.*\\]" {
        send "vtom\r"
        exp_continue
    }
    -re "Repertoire de la base de donnees.*\\]" {
        send "$install_dir/bases\r"
        exp_continue
    }
    -re "Repertoire de backup.*\\]" {
        send "$install_dir/backup\r"
        exp_continue
    }
    -re "Repertoire des traces.*\\]" {
        send "$install_dir/traces\r"
        exp_continue
    }
    -re "Repertoire des fichiers logs.*\\]" {
        send "$install_dir/logs\r"
        exp_continue
    }
    -re "Sauvegarder l'installation existante.*\\]" {
        send "o\r"
        exp_continue
    }
    -re "Confirmez-vous ces parametres.*\\]" {
        send "o\r"
        exp_continue
    }
    -re "sinon appuyez sur la touche Entree.*" {
        send "\r"
        exp_continue
    }
    eof {
        puts "Script terminé"
        exit
    }
    timeout {
        puts "Timeout atteint"
        exit 1
    }
}

