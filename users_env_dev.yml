-- =========================================================
-- Drop user (nominatif uniquement)
-- Env  : {{ lookup('env','ENV') | upper }}
-- User : {{ item.login | upper }}
-- =========================================================

SET SERVEROUTPUT ON SIZE UNLIMITED
SET FEEDBACK OFF
SET VERIFY OFF
SET HEADING OFF
SET ECHO OFF
WHENEVER SQLERROR EXIT SQL.SQLCODE

DECLARE
  v_user    VARCHAR2(128) := UPPER('{{ item.login }}');
  v_exists  NUMBER := 0;
BEGIN
  -- Vérifier si l'utilisateur existe
  SELECT COUNT(*) INTO v_exists
  FROM dba_users
  WHERE username = v_user;

  IF v_exists = 0 THEN
    DBMS_OUTPUT.PUT_LINE('User ' || v_user || ' does not exist; nothing to drop.');
    RETURN;
  END IF;

  DBMS_OUTPUT.PUT_LINE('User ' || v_user || ' exists; searching sessions to kill...');

  -- Tuer toutes les sessions de ce user (connexion directe)
  FOR s IN (
    SELECT sid, serial#, inst_id
    FROM   gv$session
    WHERE  username = v_user
    AND    type = 'USER'
    AND    username IS NOT NULL
  ) LOOP
    BEGIN
      DBMS_OUTPUT.PUT_LINE(
        'Killing session ' || s.sid || ',' || s.serial# || ' @ ' || s.inst_id || ' ...'
      );

      EXECUTE IMMEDIATE
        'ALTER SYSTEM KILL SESSION ''' ||
          s.sid || ',' || s.serial# || ',@' || s.inst_id || ''' IMMEDIATE';

    EXCEPTION
      WHEN OTHERS THEN
        DBMS_OUTPUT.PUT_LINE(
          'Could not kill session ' || s.sid || ',' || s.serial# || ' @ ' || s.inst_id ||
          ' : ' || SQLERRM
        );
    END;
  END LOOP;

  -- Petite pause pour laisser Oracle nettoyer les sessions tuées
  BEGIN
    DBMS_LOCK.SLEEP(1);
  EXCEPTION
    WHEN OTHERS THEN NULL;
  END;

  -- Tentative de DROP
  DBMS_OUTPUT.PUT_LINE('Dropping user ' || v_user || ' CASCADE ...');

  BEGIN
    EXECUTE IMMEDIATE 'DROP USER ' || v_user || ' CASCADE';
    DBMS_OUTPUT.PUT_LINE('User ' || v_user || ' dropped.');
  EXCEPTION
    WHEN OTHERS THEN
      IF SQLCODE = -1940 THEN
        -- User encore connecté (possiblement via un proxy ou autre cas particulier)
        DBMS_OUTPUT.PUT_LINE(
          'ERROR dropping ' || v_user ||
          ' : user is still connected (ORA-01940). Please disconnect remaining sessions (direct or proxy) and rerun.'
        );
      ELSE
        DBMS_OUTPUT.PUT_LINE('ERROR dropping ' || v_user || ' : ' || SQLERRM);
      END IF;
      RAISE;
  END;

END;
/
