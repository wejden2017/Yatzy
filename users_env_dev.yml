DECLARE
  v_user    VARCHAR2(128) := UPPER('{{ item.login }}');
  v_exists  NUMBER := 0;
BEGIN
  SELECT COUNT(*)
    INTO v_exists
    FROM dba_users
   WHERE username = v_user;

  IF v_exists = 0 THEN
    DBMS_OUTPUT.PUT_LINE('User '||v_user||' does not exist; nothing to drop.');
    RETURN;
  END IF;

  DBMS_OUTPUT.PUT_LINE('User '||v_user||' exists; searching sessions to kill...');

  FOR s IN (
    SELECT sid, serial#, inst_id
      FROM gv$session
     WHERE username = v_user
       AND type = 'USER'
       AND username IS NOT NULL
  ) LOOP
    BEGIN
      DBMS_OUTPUT.PUT_LINE(
        'Killing session '||s.sid||','||s.serial#||' @ '||s.inst_id||'...'
      );
      EXECUTE IMMEDIATE
        'ALTER SYSTEM KILL SESSION '''||s.sid||','||s.serial#||
        ',@'||s.inst_id||''' IMMEDIATE';
    EXCEPTION
      WHEN OTHERS THEN
        DBMS_OUTPUT.PUT_LINE(
          'Could not kill session '||s.sid||','||s.serial#||' : '||SQLERRM
        );
    END;
  END LOOP;

  BEGIN
    DBMS_LOCK.SLEEP(1);
  EXCEPTION
    WHEN OTHERS THEN NULL;
  END;

  DBMS_OUTPUT.PUT_LINE('Dropping user '||v_user||' CASCADE ...');
  EXECUTE IMMEDIATE 'DROP USER '||v_user||' CASCADE';
  DBMS_OUTPUT.PUT_LINE('User '||v_user||' dropped.');
EXCEPTION
  WHEN OTHERS THEN
    DBMS_OUTPUT.PUT_LINE('ERROR dropping '||v_user||' : '||SQLERRM);
    RAISE;
END;
/
